apply plugin: 'com.android.application'
apply plugin: 'android-apt'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    def VERSION_CODE = 9
    def VERSION_NAME = "0.2.4"
    def APP_NAME = 'Froody'
    def APP_ICON ="@drawable/ic_launcher"
    def APP_PACKAGE = "io.github.froodyapp"

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 25

        buildConfigField "boolean", "IS_TEST_BUILD", "false"
        buildConfigField "boolean", "IS_GPLAY_BUILD", "false"
        //vectorDrawables.useSupportLibrary = true
    }

    productFlavors {
        flavorDefault {
            applicationId APP_PACKAGE
            resValue 'string', 'app_name', APP_NAME
            manifestPlaceholders = [appIcon: APP_ICON]
            versionCode VERSION_CODE ; versionName VERSION_NAME
        }

        flavorTest {
            applicationId APP_PACKAGE + ".test"
            resValue 'string', 'app_name', '"Frooty"'
            manifestPlaceholders = [appIcon: APP_ICON + "_test"]
            versionCode = Integer.parseInt(new Date().format('yyMMdd'))
            versionName = new Date().format('yyMMdd')
            buildConfigField "boolean", "IS_TEST_BUILD", "true"
        }

        flavorGplay {
            applicationId APP_PACKAGE
            resValue 'string', 'app_name', APP_NAME
            manifestPlaceholders = [appIcon: APP_ICON]
            versionCode VERSION_CODE ; versionName VERSION_NAME
            buildConfigField "boolean", "IS_GPLAY_BUILD", "true"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        warning 'InvalidPackage'
        disable 'MissingTranslation'
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/DEPENDENCIES'
    }
}

// Additional repositories
repositories {
    maven {
        url "https://jitpack.io"
    }
    jcenter()
    mavenCentral()
}

dependencies {
    // Sub-Projects
    compile(project(':swagger')) {
        exclude module: "httpmime"
        exclude module: "httpcore"
        exclude module: "httpclient"
    }

    // Jars
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'

    // Android standard libs
    compile 'com.android.support:appcompat-v7:25.2.0'
    compile 'com.android.support:design:25.2.0'
    compile 'com.android.support:support-v4:25.2.0'

    // Open Street Map & Geo
    compile 'org.osmdroid:osmdroid-android:5.6.3'
    compile 'com.github.MKergall:osmbonuspack:6.2'
    compile 'ch.hsr:geohash:1.3.0'

    // More libraries
    compile 'com.jakewharton:butterknife:8.0.1'
    apt 'com.jakewharton:butterknife-compiler:8.0.1'
}

final String RAW_DIR = "app/src/main/res/raw"
final String[] ROOT_TO_RAW_COPYFILES = ["README.md", "LICENSE.md", "CHANGELOG.md", "CONTRIBUTORS.txt"]

// Called before building
task copyRepoFiles(type: Copy) {

    // Copy files over to raw dir
    from rootProject.files(ROOT_TO_RAW_COPYFILES)
    into RAW_DIR.replaceFirst("app/", "")
    rename { String fileName ->
        fileName.replace(fileName, fileName.toLowerCase())
    }

    // Filter Contributors file
    from(rootProject.file("CONTRIBUTORS.txt")) {
        into '.'    // Target already changed to 'src/main/res/raw'
        rename { String fileName ->
            fileName.replace(fileName, fileName.toLowerCase())
        }
        filter { line ->
            (line.toString().matches("..l>>.*") || line.toString().startsWith("## 99l CONTRIBUTORS")) ? null : line.toString().trim().replaceAll(" \\(.*\\)", "")
        }
    }
}

tasks.copyRepoFiles.execute()
